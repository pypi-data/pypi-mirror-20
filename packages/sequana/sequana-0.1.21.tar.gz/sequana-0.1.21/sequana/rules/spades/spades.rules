rule spades:
    """ Spade is a de novo assembler designed for small genomes like bacteria
    or fungi. This rule works only with paired-end files.

    Required input:
        __spades__input: list of paired fastq files

    Required output:
        __spades__contigs: fasta file
        __spades__scaffolds: fasta file

    Required parameters:
        __spades__outdir: output directory
        config["spades"]["threads"]: number of threads

    Contributors:
        Sequana consortium
    """
    input:
        fastq = __spades__input
    output:
        contigs = __spades__contigs,
        scaffolds = __spades__scaffolds
    log:
        __spades__log
    threads:
        config["spades"]["threads"]
    params:
        options = config["spades"]["options"],
        outdir = __spades__outdir
    shell:
        """
        # spades
        spades.py -t {threads} {params.options} -1 {input.fastq[0]} \
            -2 {input.fastq[1]} -o {params.outdir} > {log} 2>&1

        # rename assembly files
        mv {params.outdir}/scaffolds.fasta {output.scaffolds}
        mv {params.outdir}/contigs.fasta {output.contigs}
        """
